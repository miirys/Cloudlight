import QtQuick
import QtQuick.Controls
import OpenNOW

FocusScope {
    id: root
    property var variants: []
    property int currentIndex: 0
    property bool expanded: false
    property bool popupPresented: false
    property int highlightedIndex: 0
    signal variantSelected(int index)

    width: 504
    height: 56
    activeFocusOnTab: true
    z: popupPresented ? 100 : 0
    readonly property int boundedCurrentIndex: variants.length > 0
        ? Math.max(0, Math.min(variants.length - 1, currentIndex)) : 0
    readonly property var currentVariant: currentIndex >= 0 && currentIndex < variants.length
        ? variants[currentIndex] : ({store:qsTr("Choose platform"), libraryStatus:null})

    Accessible.role: Accessible.ComboBox
    Accessible.name: qsTr("Platform")
    Accessible.description: platformName(currentVariant)
        + ", " + ownershipText(currentVariant)

    function owned(variant) {
        return ["MANUAL", "PLATFORM_SYNC"].indexOf(variant.libraryStatus) >= 0
    }

    function ownershipText(variant) {
        return owned(variant) ? qsTr("Owned") : variant.libraryStatus === "NOT_OWNED" ? qsTr("Not owned") : qsTr("Ownership unconfirmed")
    }

    function platformName(variant) {
        return String(variant && variant.store || qsTr("Unknown platform"))
    }

    function platformGlyph(variant) {
        const name = platformName(variant).toUpperCase()
        if (name.indexOf("EPIC") >= 0) return "E"
        if (name.indexOf("UBISOFT") >= 0) return "U"
        if (name.indexOf("BATTLE") >= 0) return "B"
        if (name.indexOf("XBOX") >= 0 || name.indexOf("MICROSOFT") >= 0) return "X"
        if (name.indexOf("GOG") >= 0) return "G"
        return "S"
    }

    function platformColor(variant) {
        const glyph = platformGlyph(variant)
        if (glyph === "E") return Theme.cartEpic
        if (glyph === "U") return Theme.cartUbisoft
        if (glyph === "B") return Theme.cartBattlenet
        if (glyph === "X") return Theme.cartXbox
        if (glyph === "G") return Theme.cartGog
        return Theme.cartSteam
    }

    function openMenu() {
        if (!variants.length)
            return
        highlightedIndex = boundedCurrentIndex
        expanded = true
        forceActiveFocus()
    }

    function closeMenu() {
        expanded = false
        forceActiveFocus()
    }

    function choose(index) {
        const bounded = Math.max(0, Math.min(variants.length - 1, index))
        variantSelected(bounded)
        closeMenu()
    }

    onExpandedChanged: {
        if (expanded) {
            closeTimer.stop()
            popupPresented = true
        } else if (popupPresented) {
            closeTimer.restart()
        }
    }
    onCurrentIndexChanged: highlightedIndex = boundedCurrentIndex

    Keys.onPressed: event => {
        if (!expanded) {
            if (event.key === Qt.Key_Return || event.key === Qt.Key_Enter
                    || event.key === Qt.Key_Space) {
                openMenu()
                event.accepted = true
            }
            return
        }
        if (event.key === Qt.Key_Up)
            highlightedIndex = (highlightedIndex - 1 + variants.length) % variants.length
        else if (event.key === Qt.Key_Down)
            highlightedIndex = (highlightedIndex + 1) % variants.length
        else if (event.key === Qt.Key_Return || event.key === Qt.Key_Enter
                 || event.key === Qt.Key_Space)
            choose(highlightedIndex)
        else if (event.key === Qt.Key_Escape || event.key === Qt.Key_Back)
            closeMenu()
        else
            return
        event.accepted = true
    }

    Timer {
        id: closeTimer
        interval: Theme.overlayDuration
        repeat: false
        onTriggered: root.popupPresented = false
    }

    Rectangle {
        anchors.fill: parent
        radius: Theme.radiusLarge
        color: Theme.glassStrong
        border.color: root.activeFocus || root.expanded ? Theme.focus : Theme.seam
        border.width: root.activeFocus || root.expanded ? 3 : 1

        StoreBadge {
            x: 14
            anchors.verticalCenter: parent.verticalCenter
            storeGlyph: root.platformGlyph(root.currentVariant)
            storeColor: root.platformColor(root.currentVariant)
        }
        Column {
            x: 56
            anchors.verticalCenter: parent.verticalCenter
            spacing: 1
            Text {
                text: root.platformName(root.currentVariant)
                color: Theme.label
                font.family: Theme.bodyFont
                font.pixelSize: 16
                font.weight: Font.Bold
            }
            Text {
                text: root.ownershipText(root.currentVariant)
                color: root.owned(root.currentVariant) ? Theme.mint : Theme.textMuted
                font.family: Theme.bodyFont
                font.pixelSize: 13
                font.weight: Font.DemiBold
            }
        }
        Text {
            anchors.right: parent.right
            anchors.rightMargin: 18
            anchors.verticalCenter: parent.verticalCenter
            text: "⌄"
            color: Theme.label
            font.pixelSize: 20
            font.weight: Font.Bold
            rotation: root.expanded ? 180 : 0
            Behavior on rotation { NumberAnimation { duration: Theme.focusDuration; easing.type: Easing.OutCubic } }
        }
        TapHandler { onTapped: root.expanded ? root.closeMenu() : root.openMenu() }
    }

    Rectangle {
        visible: root.popupPresented
        x: 7
        y: 70
        width: parent.width
        height: Math.min(304, 16 + root.variants.length * 56)
        radius: Theme.radiusLarge
        color: Theme.surfaceRaised
        border.color: Theme.seam
        opacity: root.expanded ? 1 : 0
        scale: root.expanded ? 1 : 0.96
        transformOrigin: Item.TopRight
        Behavior on opacity { NumberAnimation { duration: Theme.overlayDuration; easing.type: Easing.OutCubic } }
        Behavior on scale { NumberAnimation { duration: Theme.overlayDuration; easing.type: Easing.OutCubic } }
    }

    GlassPanel {
        visible: root.popupPresented
        x: 0
        y: 64
        width: parent.width
        height: Math.min(304, 16 + root.variants.length * 56)
        panelRadius: 26
        strong: true
        color: "#10131C"
        opacity: root.expanded ? 1 : 0
        scale: root.expanded ? 1 : 0.96
        transformOrigin: Item.TopRight
        Behavior on opacity { NumberAnimation { duration: Theme.overlayDuration; easing.type: Easing.OutCubic } }
        Behavior on scale { NumberAnimation { duration: Theme.overlayDuration; easing.type: Easing.OutCubic } }

        ListView {
            anchors.fill: parent
            anchors.margins: 8
            spacing: 2
            clip: true
            interactive: contentHeight > height
            model: root.variants
            currentIndex: root.highlightedIndex
            delegate: ItemDelegate {
                id: platformOption
                required property var modelData
                required property int index
                width: ListView.view.width
                height: 54
                padding: 0
                highlighted: index === root.highlightedIndex
                Accessible.name: root.platformName(modelData)
                Accessible.description: root.ownershipText(modelData)
                onClicked: root.choose(index)
                background: Rectangle {
                    radius: Theme.radiusLarge
                    color: platformOption.highlighted ? Theme.face : "transparent"
                    border.color: platformOption.highlighted ? Theme.focus : "transparent"
                    border.width: platformOption.highlighted ? 2 : 0
                }
                contentItem: Item {
                    StoreBadge {
                        x: 10
                        anchors.verticalCenter: parent.verticalCenter
                        storeGlyph: root.platformGlyph(modelData)
                        storeColor: root.platformColor(modelData)
                    }
                    Text {
                        x: 52
                        anchors.verticalCenter: parent.verticalCenter
                        width: parent.width - x - ownership.width - 32
                        text: root.platformName(modelData)
                        color: platformOption.highlighted ? Theme.faceText : Theme.label
                        elide: Text.ElideRight
                        font.family: Theme.bodyFont
                        font.pixelSize: 15
                        font.weight: Font.Bold
                    }
                    Rectangle {
                        id: ownership
                        anchors.right: parent.right
                        anchors.rightMargin: 12
                        anchors.verticalCenter: parent.verticalCenter
                        width: ownershipText.implicitWidth + 18
                        height: 30
                        radius: Theme.radiusLarge
                        color: root.owned(modelData)
                            ? (platformOption.highlighted ? Theme.surfaceHover : Theme.surfaceStrong)
                            : "transparent"
                        border.color: root.owned(modelData) ? Theme.mint : Theme.seam
                        border.width: 1
                        Text {
                            id: ownershipText
                            anchors.centerIn: parent
                            text: root.ownershipText(modelData)
                            color: root.owned(modelData)
                                ? (platformOption.highlighted ? Theme.faceText : Theme.mint)
                                : (platformOption.highlighted ? "#5C5C5C" : Theme.textMuted)
                            font.family: Theme.bodyFont
                            font.pixelSize: 12
                            font.weight: Font.Bold
                        }
                    }
                }
            }
        }
    }
}
