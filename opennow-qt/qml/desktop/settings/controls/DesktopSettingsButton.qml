import QtQuick
import QtQuick.Controls
import OpenNOW

Button {
    id: control
    property bool primary: false
    property bool danger: false
    property bool compact: false
    property bool menu: false
    property string suffix: ""
    property string keySequence: ""

    implicitHeight: compact ? DesktopTokens.px(28) : DesktopTokens.px(32)
    implicitWidth: Math.max(DesktopTokens.px(compact ? 68 : 84), (keySequence !== "" ? bindingGlyph.implicitWidth : label.implicitWidth) + leftPadding + rightPadding
        + (menu ? DesktopTokens.px(22) : 0) + (suffix !== "" ? suffixGlyph.implicitWidth + DesktopTokens.px(8) : 0))
    hoverEnabled: true
    padding: 0
    leftPadding: DesktopTokens.px(14)
    rightPadding: DesktopTokens.px(14)
    topPadding: 0
    bottomPadding: 0

    background: Rectangle {
        radius: DesktopTokens.radius
        color: control.primary ? (control.down ? Qt.darker(Theme.focus, 1.15) : control.hovered ? Qt.lighter(Theme.focus, 1.08) : Theme.focus)
             : control.down || control.hovered ? Theme.surfaceHover : Theme.surfaceRaised
        border.width: control.activeFocus ? 2 : control.primary ? 0 : 1
        border.color: control.activeFocus ? Theme.label
                    : control.danger ? Theme.coral
                    : Theme.seam
        Behavior on color { ColorAnimation { duration: Theme.focusDuration } }
    }

    contentItem: Item {
        implicitWidth: contentRow.implicitWidth
        implicitHeight: contentRow.implicitHeight
        Row {
            id: contentRow
            anchors.centerIn: parent
            spacing: DesktopTokens.px(8)
            Text {
                id: label
                objectName: "settingsButtonLabel"
                visible: control.keySequence === ""
                text: control.text
                width: Math.max(0, Math.min(implicitWidth, control.availableWidth
                    - (control.menu ? DesktopTokens.px(22) : 0) - (control.suffix !== "" ? suffixGlyph.implicitWidth + DesktopTokens.px(8) : 0)))
                elide: Text.ElideRight
                color: control.primary ? Theme.focusText : control.danger ? Theme.coral : Theme.label
                font.family: Theme.bodyFont
                font.pixelSize: DesktopTokens.px(13)
                font.weight: control.primary ? Font.DemiBold : Font.Medium
                anchors.verticalCenter: parent.verticalCenter
            }
            KeyboardGlyph {
                id: bindingGlyph
                visible: control.keySequence !== ""
                shortcut: control.keySequence
                keySize: DesktopTokens.px(22)
                ink: control.primary ? Theme.focusText : Theme.label
                anchors.verticalCenter: parent.verticalCenter
            }
            KeyboardGlyph {
                id: suffixGlyph
                visible: control.suffix !== ""
                shortcut: control.suffix
                keySize: DesktopTokens.px(18)
                ink: control.primary ? Theme.focusText : Theme.textMuted
                anchors.verticalCenter: parent.verticalCenter
            }
            DesktopSettingsIcon {
                visible: control.menu
                width: DesktopTokens.px(10)
                height: width
                glyph: "chevron"; rotation: 90
                ink: control.primary ? Theme.focusText : Theme.textMuted
                anchors.verticalCenter: parent.verticalCenter
            }
        }
    }
}
